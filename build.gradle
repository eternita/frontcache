import org.akhikhl.gretty.AppBeforeIntegrationTestTask


buildscript {
	repositories {
	  jcenter()
	}
  
	dependencies {
	  classpath 'org.akhikhl.gretty:gretty:+' // use gretty to run Jetty9 (servlet 3.1.0 container)
	}
  }
  
  
subprojects {
    apply plugin: 'java'
	
    version = '0.4.0-SNAPSHOT'
    group = 'frontcache'
	
	configurations {
		provided
	}
	 
	sourceSets {
		main {
			compileClasspath += configurations.provided
			test.compileClasspath += configurations.provided
			test.runtimeClasspath += configurations.provided
		}
	}
	
    dependencies {
		testCompile 'junit:junit:4.12'
    }
	
    repositories {
        mavenLocal()
        mavenCentral()
    }
    
	tasks.withType(Test) {
		testLogging {
			showStandardStreams = true
			exceptionFormat 'full'
		}
	}

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

project(':frontcache-core') {
	
    dependencies {
        provided         'javax.servlet:javax.servlet-api:3.1.0'
		provided         'javax.servlet.jsp:jsp-api:2.0'
		
        compile         'org.apache.httpcomponents:httpclient:4.5.1'
        compile         'commons-io:commons-io:2.4'
        compile         'org.apache.commons:commons-collections4:4.1'
        compile         'org.apache.logging.log4j:log4j-api:2.4.1'
        compile         'org.apache.logging.log4j:log4j-core:2.4.1'
    }
	
}


project(':frontcache-ehcache') {
    dependencies {
        compile project(':frontcache-core')
        compile         'net.sf.ehcache:ehcache:2.10.0'
    }
}


project(':frontcache-web') {
    apply plugin: 'war'
	apply plugin: 'org.akhikhl.gretty'

	gretty {
		port = 9080		
		statusPort = 9080		
		servicePort = 9080		
//		contextPath = '/fc-test'
		contextPath = '/'
		servletContainer = 'tomcat7' // jetty9 or tomcat7 or tomcat8
//		inplaceMode = 'hard'
//		loggingLevel = 'All'
		integrationTestTask = 'integrationTest' // existing gradle task (it will be surrounded with start/stop)
	}
	

	sourceSets {
		integrationTest {
			java.srcDir file('src/test-integration/java')
			resources.srcDir file('src/test-integration/resources')
			compileClasspath = sourceSets.main.output + configurations.testRuntime
			runtimeClasspath = output + compileClasspath
		}
	}
	
    dependencies {
        compile project(':frontcache-core')
        compile project(':frontcache-ehcache')
		
		compile 'net.sourceforge.htmlunit:htmlunit:2.18'
		compile 'jstl:jstl:1.2'
//		compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.22.2'
		
    }

	test {
		reports.html.destination = file("$reports.html.destination/unit")
		reports.junitXml.destination = file("$reports.junitXml.destination/unit")
	}
	
	task integrationTest(type: Test) {
		description = 'Runs the integration tests.'
		group = 'verification'
		testClassesDir = sourceSets.integrationTest.output.classesDir
		classpath = sourceSets.integrationTest.runtimeClasspath
		reports.html.destination = file("$reports.html.destination/integration")
		reports.junitXml.destination = file("$reports.junitXml.destination/integration")
//		dependsOn startAndPrepareDatabase
//		finalizedBy stopDatabase
	}
	
//	test {
//		include '**/Test*.*'
//		include '**/*Test.*'
//		exclude '**/*IT.*'
//	  }
//	  
//	
//	
//	task integrationTest(type: Test) { // , dependsOn: 'appStartWar'
//	  outputs.upToDateWhen { false }
//	  include '**/*IT.*'
//	
//	//  	doFirst {
//	//  		tasks.appRunWar.execute()
//	//  	}
//	  
//	}
//	
	
	check {
		dependsOn integrationTest
	}
	
}
