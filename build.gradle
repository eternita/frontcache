import org.akhikhl.gretty.AppBeforeIntegrationTestTask


buildscript {
	repositories {
	  jcenter()
	}
  
	dependencies {
	  classpath 'org.akhikhl.gretty:gretty:+' // use gretty to run Jetty9 (servlet 3.1.0 container)
	}
  }
  
  
subprojects {
    apply plugin: 'java'
	
    version = '0.4.0-SNAPSHOT'
    group = 'frontcache'
	
	configurations {
		provided
	}
	 
	sourceSets {
		main {
			compileClasspath += configurations.provided
			test.compileClasspath += configurations.provided
			test.runtimeClasspath += configurations.provided
		}
	}
	
    dependencies {
		testCompile 'junit:junit:4.12'
    }
	
    repositories {
        mavenLocal()
        mavenCentral()
    }
    
	tasks.withType(Test) {
		testLogging {
			showStandardStreams = true
			exceptionFormat 'full'
		}
	}
	
	sourceSets {
		integrationTest {
			java.srcDir file('src/test-integration/java')
			resources.srcDir file('src/test-integration/resources')
			compileClasspath = sourceSets.main.output + configurations.testRuntime
			runtimeClasspath = output + compileClasspath
		}
	}

	test {
		reports.html.destination = file("$reports.html.destination/unit")
		reports.junitXml.destination = file("$reports.junitXml.destination/unit")
		
		if (System.getProperty('DEBUG', 'false') == 'true') {
			jvmArgs '-Xdebug',
				'-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8001'
		}
	}
	
	task integrationTest(type: Test) {
		description = 'Runs the integration tests.'
		group = 'verification'
		testClassesDir = sourceSets.integrationTest.output.classesDir
		classpath = sourceSets.integrationTest.runtimeClasspath
		reports.html.destination = file("$reports.html.destination/integration")
		reports.junitXml.destination = file("$reports.junitXml.destination/integration")
		
//		if (System.getProperty('DEBUG', 'false') == 'true') {
//			jvmArgs '-Xdebug',
//				'-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8002'
//		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

project(':frontcache-core') {
	
    dependencies {
        provided         'javax.servlet:javax.servlet-api:3.1.0'
		provided         'javax.servlet.jsp:jsp-api:2.0'
		
        compile         'org.apache.httpcomponents:httpclient:4.5.1'
        compile         'commons-io:commons-io:2.4'
        compile         'org.apache.commons:commons-collections4:4.1'
        compile         'ch.qos.logback:logback-classic:1.1.3'
    }
	
}

project(':frontcache-ehcache') {
    dependencies {
        compile project(':frontcache-core')
        compile         'net.sf.ehcache:ehcache:2.10.0'
    }
}

project(':frontcache-web') {
	apply plugin: 'war'
	
    dependencies {
        compile project(':frontcache-core')
    }
}

project(':frontcache-filter-tests') {
    apply plugin: 'war'
	apply plugin: 'org.akhikhl.gretty'

	gretty {
		port = 9080		
		contextPath = '/'
		servletContainer = 'jetty9' // jetty9 or tomcat7 or tomcat8
		integrationTestTask = 'test' // existing gradle task (it will be surrounded with start/stop)
		jvmArgs = ['-Dfrontcache.home=' + projectDir +'/FRONTCACHE_HOME']
		if (System.getProperty('DEBUG', 'false') == 'true') {
			jvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8003', '-Dfrontcache.home=' + projectDir +'/FRONTCACHE_HOME']
		}
	}
	
    dependencies {
        compile project(':frontcache-core')
        compile project(':frontcache-ehcache')
		
		compile 'net.sourceforge.htmlunit:htmlunit:2.18'
		compile 'jstl:jstl:1.2'
    }
	
	check {
		dependsOn integrationTest
	}
	
}

project(':frontcache-standalone-tests') {
	apply plugin: 'war'
	apply plugin: 'org.akhikhl.gretty'

	gretty {
		port = 9080
		contextPath = '/'
		servletContainer = 'tomcat7' // jetty9 or tomcat7 or tomcat8
		integrationTestTask = 'integrationTest' // existing gradle task (it will be surrounded with start/stop)
		jvmArgs = ['-Dfrontcache.home=' + projectDir + '/FRONTCACHE_HOME']
//		if (System.getProperty('DEBUG', 'false') == 'true') {
//			jvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=8003', '-Dfrontcache.home=' + projectDir + '/FRONTCACHE_HOME']
//		}
	}
	
	integrationTest{
		jvmArgs '-Dfrontcache.standalone.test.web.dir=' + projectDir + '/src/test-integration/webapp'
	}

	dependencies {
		compile project(':frontcache-core')
//		compile project(':frontcache-ehcache') - no persistence cache for tests
		
		compile 'net.sourceforge.htmlunit:htmlunit:2.18'
		compile 'org.eclipse.jetty:jetty-server:9.3.6.v20151106'
		compile 'org.eclipse.jetty:jetty-webapp:9.3.6.v20151106'
		compile 'org.eclipse.jetty:jetty-annotations:9.3.6.v20151106'
		compile 'jstl:jstl:1.2'
	}

	check {
		dependsOn integrationTest
	}
	
}

